///$tab QDF
// QDF need to be initiated using InitLink.qvs or 1.Init.qvs
// Below ia a basic initiation, Include search for InitLink.qvs in container base folder
// InitLink.qvs finds and links to 1.Init.qvs
Let vL.InitLinkPath = Left(DocumentPath(), Index(DocumentPath(), '\99.Script'))& 'InitLink.qvs';
$(Must_Include=$(vL.InitLinkPath));

// Locale para Argentina
$(Include=$(vG.LocalePath)\13.Arg.qvs);

//ConnectionString para Oracle
$(Include=$(vG.ConnStringPath)\Conexion_ACAPROD1_ESPEJO.conn);
///$tab TIEMPOS RECARGA
TIEMPO_RECARGA:
NoConcatenate
LOAD
	Now()			as FechaHoraInicio,
	OSUser()		AS Usuario
AUTOGENERATE 1;
///$tab TABLA
LET vL.Tabla = 'SA_LIQUIDACIONES_CAPITA';
LET vL.Base = 'SALUD';
LET vL.QVD = 'DATA_ORACLE_$(vL.Base)_$(vL.Tabla)';


// Seteo el Tipo de Lectura.
SET vL.TipoLectura = 'Incremental';


// Seteo la fecha desde de lectura incremental.
LET vL.FechaDesde = Date(Today() - 365);



// Levanto los datos. 
$(vL.QVD):
SQL 
SELECT *
FROM $(vL.Base).$(vL.Tabla)
WHERE PERIODO >= to_date('$(vL.FechaDesde)', 'dd/mm/yyyy');  
  

// Seteo Variables.
LET vL.QRegistros = NoOfRows('$(vL.QVD)');
LET vL.QCampos = NoOfFields('$(vL.QVD)');



// Concateno lo guardado.
Concatenate($(vL.QVD))
LOAD *
FROM $(vG.ExtractPath)\$(vL.QVD).qvd (qvd)
WHERE Not Exists (LC_ID);


Inner Join ($(vL.QVD))
	SQL 
	SELECT 
		  LC_ID
	FROM $(vL.Base).$(vL.Tabla);


// Guardo la Resultante.
STORE $(vL.QVD) into $(vG.ExtractPath)\$(vL.QVD).qvd (qvd);

	//TEMPORARIAMENTE SE GUARDA EN ambos ambientes
	STORE $(vL.QVD) into D:\Qlikview\PRD\DATA\ORACLE\QVD\$(vL.QVD).qvd (qvd);

DROP Table $(vL.QVD);
///$tab DURACION
Left Join (TIEMPO_RECARGA)

LOAD
	now() 			AS FechaHoraFin
AUTOGENERATE 1;


TIEMPO_EJECUCION:
NoConcatenate
LOAD
	*,
	time(FechaHoraFin-FechaHoraInicio,'hh:mm:ss') 		AS Duracion,
	'$(vL.QRegistros)'									AS QRegistros,
	'$(vL.QCampos)'										AS QCampos,
	'$(vL.Tabla)'										AS Tabla,
	'$(vL.QVD)'											AS QVD,
	'$(vL.TipoLectura)'									AS TipoLectura
RESIDENT TIEMPO_RECARGA;

DROP Table TIEMPO_RECARGA;